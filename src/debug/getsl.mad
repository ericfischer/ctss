           EXTERNAL FUNCTION(NUM,BAD)
           NORMAL MODE IS INTEGER
           STATEMENT LABEL BAD
           PROGRAM COMMON LIST(84)
          R
          RGET STATEMENT LABEL FROM INPUT LIST
          R
           ENTRY TO GETSL.
          R
           LASCRD.(END)
           WHENEVER END.E.0
           PRINT.(COM3)
           VECTOR VALUES COM3=25,$NULL PROGRAM.  TRY AGAIN.$
           READY.
           END OF CONDITIONAL
          R
           WHENEVER LIST(LIST).E.$*$
           LIST=LIST+1
           NUM=STAR
           TRANSFER TO AND
           END OF CONDITIONAL
          R
           WHENEVER LIST(LIST).E.$/$
           LIST=LIST+1
           LASCRD.(NUM)
           TRANSFER TO AND
           END OF CONDITIONAL
          R
           WHENEVER LIST(LIST).RS.30.LE.9
           GETNUM.(NUM,NOREAD)
           TRANSFER TO CHECK
           END OF CONDITIONAL
          R
           GETNAM.(NAME,NOREAD)
           WHENEVER LIST(LIST).E.$($
           LIST=LIST+1
           GETNUM.(SUB,NOREAD)
           WHENEVER LIST(LIST).NE.$)$,TRANSFER TO NOREAD
           LIST=LIST+1
           OTHERWISE
           SUB=0
           END OF CONDITIONAL
          R
           SLNUM.(NUM,NAME,SUB)
           WHENEVER NUM.E.0
           LIST=0
           ADDNAM.(NAME)
           WHENEVER SUB.NE.0
           LIST=LIST+1
           LIST(LIST)=$($
           ADDNUM.(SUB)
           LIST=LIST+1
           LIST(LIST)=$)$
           END OF CONDITIONAL
           ADDCOM.(COM4)
           VECTOR VALUES COM4=29,$ IS NOT A STATEMENT LABEL IN $
           GWORKA.(WORK)
           ADDNAM.(WORK)
           ADDCOM.(COM5)
           VECTOR VALUES COM5=1,$.$
           CRTYPE.
           TRANSFER TO BAD
           END OF CONDITIONAL
AND        WHENEVER LIST(LIST).E.$+$.OR.LIST(LIST).E.$-$
           GETNUM.(SUB,NOREAD)
           NUM=NUM+SUB
           END OF CONDITIONAL
          R
CHECK      WHENEVER NUM.LE.0.OR.NUM.G.END
           PRINT.(COM1)
           VECTOR VALUES COM1=41,$STATEMENT LABEL REFERENC$,
          1$E OUT OF PROGRAM.$
           TRANSFER TO BAD
           END OF CONDITIONAL
           STAR=NUM
           FUNCTION RETURN
          R
NOREAD     PRINT.(COM2)
           VECTOR VALUES COM2=27,$STATEMENT LABEL UNREADABLE.$
           TRANSFER TO BAD
          R
          RADD STATEMENT LABEL TO OUTPUT LIST
          R
           ENTRY TO ADDSL.
           SLNAM.(NUM,NAME,SUB,DEV)
          R
           WHENEVER NAME.E.$      $
           ADDNUM.(DEV)
           FUNCTION RETURN
           END OF CONDITIONAL
           ADDNAM.(NAME)
          R
           WHENEVER SUB.NE.0
           LIST=LIST+1
           LIST(LIST)=$($
           ADDNUM.(SUB)
           LIST=LIST+1
           LIST(LIST)=$)$
           END OF CONDITIONAL
          R
           WHENEVER DEV.G.0
           LIST=LIST+1
           LIST(LIST)=$+$
           END OF CONDITIONAL
           WHENEVER DEV.NE.0,ADDNUM.(DEV)
           FUNCTION RETURN
           END OF FUNCTION
