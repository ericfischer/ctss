           EXTERNAL FUNCTION
           NORMAL MODE IS INTEGER
           BOOLEAN SPACEP,CRP
           PROGRAM COMMON LIST(84)
           VECTOR VALUES COUNT=20
          R
          RTABLES OF FIRST LETTERS, REMAINING LETTERS, TRANSFERS.
          R
           VECTOR VALUES FIRST=$A$,$B$,$C$,$D$,$E$,$F$,$G$,
          1$I$,$K$,$L$,$M$,$O$,$P$,$Q$,$R$,$S$,$T$,$U$,$V$,$W$
           VECTOR VALUES REMAIN=$PPEND$,$REAK$,$HANGE$,$ELETE$,
          1$XECUTE$,$ORCE$,$O$,$NSERT$,$ILL$,$INKAGE$,$ANIPUL$,
          2$PEN$,$RINT$,$UIT$,$ESTORE$,$AVE$,$RANSLA$,$SE$,
          3$ERIFY$,$ORK$
           VECTOR VALUES XFER=A.,B.,C.,D.,E.,F.,G.,I.,K.,L.,
          1M.,O.,P.,Q.,R.,S.,T.,U.,V.,W.
          R
          R
          RENTRY TO HANDLE FLOATING POINT TRAPS
          R
           ENTRY TO FPTRAP.
           PRINT.(COM3)
           VECTOR VALUES COM3=32,$FLOATING POINT TRAP.  TRY AGAIN.$
           TRANSFER TO GO
          R
          RDUMMY EXIT PROGRAM SHOULDN'T BE CALLED
          R
           ENTRY TO EXIT.
           PRINT.(COM2)
           VECTOR VALUES COM2=31,$EXIT CALLED.  CONSULT LISTINGS.$
          R
          RPROCESS FIRST BCD STRING IN REQUEST LINE
          RCALL PROPER SUBROUTINE TO PROCESS THE REQUEST.
          R
           ENTRY TO READY.
GO         READ.
           OSPACE.
           WHENEVER CRP.(0),READY.
           THROUGH LOOKUP,FOR INDEX=0,1,INDEX.G.COUNT
LOOKUP     WHENEVER FIRST(INDEX).E.LIST(LIST),TRANSFER TO FOUND
           TRANSFER TO NOFIND
FOUND      LIST=LIST+1
           WHENEVER SPACEP.(0),XFER(INDEX).
           GETBCD.(BCD)
           WHENEVER BCD.NE.REMAIN(INDEX),TRANSFER TO NOFIND
          R
           WHENEVER FIRST(INDEX).E.$M$
           WHENEVER SPACEP.(0),TRANSFER TO NOFIND
           GETBCD.(BCD)
           WHENEVER BCD.NE.$ATE$,TRANSFER TO NOFIND
           OR WHENEVER FIRST(INDEX).E.$T$
           WHENEVER SPACEP.(0),TRANSFER TO NOFIND
           GETBCD.(BCD)
           WHENEVER BCD.NE.$TE$,TRANSFER TO NOFIND
           END OF CONDITIONAL
          R
           WHENEVER SPACEP.(0),XFER(INDEX).
          R
NOFIND     WHENEVER BCD.E.$ESTOR$.AND.FIRST(INDEX).E.$R$,XFER(INDEX).
           LIST=1
           OSPACE.
           THROUGH NULL,FOR II=1,1,II.E.LIST
NULL       LIST(II)=576060606060K
SKIP       THROUGH SKIP,FOR LIST=LIST,1,LIST.G.43.OR.SPACEP.(0)
           LIST=LIST-1
           ADDCOM.(COM1)
           VECTOR VALUES COM1=30,$ IS NOT A REQUEST.  TRY AGAIN.$
           CRTYPE.
           READY.
           END OF FUNCTION
