          R THE FORMAT OF THE 'SUPER LINE MARK' IN ARCHIV FILES
          R IS:
          R 777777000000K,..,..,..,777777000011K,FLN1,FLN2,
          R $ MM/DD/YY HHMM.M  $,
          R $ZZZZZZ$,$$,$   000$,$00    $
          R TOTALLING 14 WORDS, WHERE ZZZZZZ IS FILE WORD COUNT.
          R THIS INFORMATION SHLD NOT BE KNOWN ANYWHERE IN THE PROGRAM
          R EXCEPT IN THE FLLWING INTERNAL FUNCTIONS AND V'S STATEMENTS.
          R
          R MKLNMK.(A,B,CT) MAKS A LINE MARK FOR A B IN NWLNMK AND SETS
          R CT=FILE SIZE IN WORDS.  MKLNMK CALLS FSTATE.
          R
          R MKLNMK. RETURNS 1B IFF FILE EXISTS.
          R
           BOOLEAN MKLNMK.
           INTERNAL FUNCTION (A,B,CT)
           E'O MKLNMK.
           OLDERR=FERRTN.(NTFND)
           FSTATE.(A,B,FSBUF(8)...8)
           FERRTN.(OLDERR)
           CT=FSBUF(8)
           V'S NWLNMK(11)...NWLNMK(14)=777777000000K
           V'S NWLNMK(10)=777777000011K
           NWLNMK(9)=A
           NWLNMK(8)=B
           W'R .NOT. GOTTM
             BOOLEAN GOTTM
                GETTM.(PDATE,PTIME)
                NWLNMK(7)=(PDATE.RS.6).V.$ 00000$
                NWLNMK(6) = (DEFBC.(GTDYTM.(0).RS.27).LS.18)
          1          .V.(PTIME.RS.24)
          2               .V.$/00 00$
          R
                NWLNMK(5)=(PTIME.LS.12).V.$0000  $
                GOTTM=1B
           E'L
           NWLNMK(4)=BZEL.(DEFBC.(FSBUF(8)))
           V'S NWLNMK(3)=$      $
           V'S NWLNMK(2)=$   000$
           V'S NWLNMK(1)=$00    $
           F'N 1B
NTFND      FERRTN.(OLDERR)
           F'N 0B
           END OF FUNCTION
          R
          R BDLNMK.(LMK) RETURNS 1B IF LMK(LMKZ)...LMKZ IS 'BAD,'
          R THAT IS, NOT A VALID LINE MARK.
          R
           BOOLEAN BDLNMK.
           INTERNAL FUNCTION(LMK)
           E'O BDLNMK.
           W'R (LMK(14).NE.777777000000K .AND. LMK(14).NE.0)
          1.OR.LMK(13).NE.777777000000K
          2.OR.LMK(12).NE.777777000000K
          3.OR.LMK(11).NE.777777000000K
          4.OR.LMK(10).NE.777777000011K
          5.OR.LMK(7).A.776060776060K .NE. $ 00/00$
          6.OR.LMK(6).A.776060776060K .NE. $/00 00$
          7.OR.LMK(5).A.606077607777K .NE. $00.0  $
          8.OR.LMK(4).A.60K.NE.0
                F'N 1B
           O'E
                F'N 0B
           E'L
           END OF FUNCTION
          R
          R THE FOLLOWING DINK ROUTINES GET VARIOUS PIECES
          R OF INFORMATION OUT OF A LINE MARK.
          R
          R DATTM.(LMK) GETS DATE AND TIME IN FSTATE. FORMAT.
          R
           INTERNAL FUNCTION DATTM.(LMK)=
          1     (BCDEC.((LMK(6).RS.18).A.7777K).LS.27).A.777K9
          2  .V.(BCDEC.((LMK(7).RS.18).A.7777K).LS.23).A.74K7
          3  .V.(BCDEC.(LMK(7).A.7777K).LS.18).A.37K6
          4  .V.(BCDEC.(LMK(6).A.7777K)*3600
          6     +BCDEC.(LMK(5).RS.24)*60
          5     +BCDEC.((LMK(5).RS.12).A.77K)*6)
          R
          R CNT.(LMK) GETS FILE WORD COUNT
          R FLN1.(LMK) AND FLN2.(LMK) GET RESPECTIVE FILE NAMES
          R
           INTERNAL FUNCTION CNT.(LMK)=BCDEC.(LMK(4))
           INTERNAL FUNCTION FLN1.(LMK)=LMK(9)
           INTERNAL FUNCTION FLN2.(LMK)=LMK(8)
          R
          R INF1 AND INF2 ARE LIMITS OF INTEREST FOR PRINT AND TABLE.
          R LMKZ IS LINE MARK SIZE
          R PRLNMK IS 'PRESENT LINE MARK,' READ FROM ARCHIV FILE.
          R NWLNMK IS 'NEW LINE MARK,' GENERATED BY MKLNMK.
          R
           V'S INF1=9
           V'S INF2=4
           D'N PRLNMK(14), NWLNMK(14)
           V'S LMKZ=14
           D'N FSBUF(432)
