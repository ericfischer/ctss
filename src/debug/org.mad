           EXTERNAL FUNCTION (A1,A2,A3)
           NORMAL MODE IS INTEGER
           BOOLEAN WSEEK,COND
           PROGRAM COMMON LIST(84),MAX,FROM(20),TO(20),
          1FSUB(20),TSUB(20),USES(60),CURCOR,PATCH,SKIP
          R
          RCALCULATE THE TRUTH OF 'DATA IS NOT AN ENTRY NAME'
          R
           INTERNAL FUNCTION
           ENTRY TO COND.
           FUNCTION RETURN DATA.A..N.777K6.E.0.AND.DATA.NE.0 .OR.
          1DATA.E.7K11
           END OF FUNCTION
          R
          RRETURN ABSOLUTE LOCATION OF ENTRY NAME 'A1'.
          R
           ENTRY TO ENTRY.
           MSETUP.
           WHENEVER A1.E.$(MAIN)$
           A1A=0
           OTHERWISE
           A1A=A1
           END OF CONDITIONAL
           THROUGH ELOOP,FOR I=SIZE-3,-2,I.L.1
ELOOP      WHENEVER WGET.(I).E.A1A,FUNCTION RETURN WGET.(I+1)
           FUNCTION RETURN 0
          R
          RRETURN ENTRY NAME FOR ABSOLUTE LOCATION 'A1'.
          R
           ENTRY TO ENAME.
           MSETUP.
           A1A=A1.A.77777K
           THROUGH ENLOOP, FOR I=SIZE-2,-2,I.L.1
           WHENEVER WGET.(I).E.A1A
           DATA=WGET.(I-1)
           WHENEVER COND.(0), TRANSFER TO ENLOOP
           WHENEVER DATA.E.0,FUNCTION RETURN $(MAIN)$
           FUNCTION RETURN DATA
ENLOOP     END OF CONDITIONAL
           FUNCTION RETURN 0
          R
          RRETURN NAME OF USE FILE OR ENTRY POINT FOR PROGRAM
          RCONTAINING ABSOLUTE LOCATION 'A1'.
          R
           ENTRY TO ORGNAM.
           MSETUP.
           COUNT=0
           NAME=$(ZERO)$
           THROUGH OLOOP,FOR I=SIZE-1,-2,I.L.1
           DATA=WGET.(I)
          R
           WHENEVER COND.(0)
           WHENEVER WGET.(I+1).GE.A1,FUNCTION RETURN NAME
           COUNT=COUNT+1
          R
           WHENEVER COUNT.LE.USES(0).AND.COUNT.G.1
           NAME=USES(COUNT-1)
           LTJUST.(NAME)
           OTHERWISE
           NAME=WGET.(I-2)
           WHENEVER NAME.E.0,NAME=$(MAIN)$
           WHENEVER NAME.E.777777777777K,NAME=$(END)$
           END OF CONDITIONAL
          R
OLOOP      END OF CONDITIONAL
          R
           FUNCTION RETURN NAME
          R
          RSET A2,A3 TO THE START, LENGTH OF SUBROUTINE WITH USE NAME
          ROR ENTRY POINT NAME A1.
          R
           BOOLEAN NEFLAG
           ENTRY TO ORG.
           MSETUP.
           A1A=A1
           WHENEVER A1A.E.$(ZERO)$
           A2=0
           A3=0
           FUNCTION RETURN
           OR WHENEVER A1A.E.$(MAIN)$
           A1A=0
           OR WHENEVER A1A.E.$(END)$
           A2=WGET.(2)
           A3=0
           FUNCTION RETURN
           END OF CONDITIONAL
           RTJUST.(A1A)
           THROUGH NLOOP,FOR I=1,1,I.E.USES(0)
NLOOP      WHENEVER A1A.E.USES(I),TRANSFER TO USED
          R
           LTJUST.(A1A)
           NEFLAG=1B
           THROUGH NELOOP,FOR I=SIZE-1,-2,I.L.1
           DATA=WGET.(I)
           WHENEVER DATA.E.A1A
           NEFLAG=0B
           OR WHENEVER COND.(0)
           WHENEVER NEFLAG
           A2=WGET.(I+1)
           OTHERWISE
           A3=WGET.(I+1)-A2
           FUNCTION RETURN
           END OF CONDITIONAL
NELOOP     END OF CONDITIONAL
           EXIT.
          R
USED       THROUGH ULOOP,FOR J=SIZE-5,-2,J.L.1
           DATA=WGET.(J)
           WHENEVER COND.(0)
           WHENEVER I.E.1
           A2=WGET.(J+1)
           OR WHENEVER I.E.0
           A3=WGET.(J+1)-A2
           FUNCTION RETURN
           END OF CONDITIONAL
           I=I-1
ULOOP      END OF CONDITIONAL
           EXIT.
          R
          RSETUP '(MOVIE' IN WINDOW, IF NOT ALREADY THERE.
          R
           INTERNAL FUNCTION
           ENTRY TO MSETUP.
           WHENEVER WSEEK.($(MOVIE$),FUNCTION RETURN
          R
           WSETUP.($(MOVIE$,MOVIE,0)
           VECTOR VALUES MOVIE=$(MOVIETABLE)$
           WSIZE.(SIZE)
           COM(1)=WGET.(SIZE).A.77777K
           WHENEVER COM(1).A.1.E.1,COM(1)=COM(1)-1
           VECTOR VALUES COM=$(CMMN)$,0,7K11,30K
           WINS.(SIZE+1,4,COM)
           WHENEVER WGET.(SIZE-3).E.$MOVIE)$
           THROUGH GMOV,FOR I=0,1,I.E.4
GMOV       MOV(I)=WGET.(SIZE-3+I)
           WINS.(5,4,MOV)
           WDEL.(SIZE+1,4)
           DIMENSION MOV(3)
           END OF CONDITIONAL
           SIZE=SIZE+4
           FUNCTION RETURN
           END OF FUNCTION
           END OF FUNCTION
