// PREPRO.BCPL 02/13/68 09:28

get ' HEAD1'

let Rch() be
       $( if Ch='*n' do LineP := 0
          Readch(INPUT, lv Ch)
          LineP := LineP + 1
          if LineP gr LineT do LineP := LineT
          LineV*(LineP) := Ch  $)

and PPitem(Item, Class, NLpending) be
       $( Symb := Item
          if ST ne 0 do
                    test NLpending
                        then if Class ge 10 do
                                     $( Nsymb, B := Symb, true
                                        Symb := SEMICOLON  $)
                          or if ST=1 & Class ge 20 do
                                     $( Nsymb, B := Symb, true
                                        Symb := DO  $)
          NLpending, ST := false, Class rem 10   $)

and Kind(Ch) = valof
    $( switchon Ch into
       $( case 'a':case 'b':case 'c':case 'd':case 'e':
          case 'f':case 'g':case 'h':case 'i':case 'j':
          case 'k':case 'l':case 'm':case 'n':case 'o':
          case 'p':case 'q':case 'r':case 's':case 't':
          case 'u':case 'v':case 'w':case 'x':case 'y':
          case 'z':
                    resultis Small

          case 'A':case 'B':case 'C':case 'D':case 'E':
          case 'F':case 'G':case 'H':case 'I':case 'J':
          case 'K':case 'L':case 'M':case 'N':case 'O':
          case 'P':case 'Q':case 'R':case 'S':case 'T':
          case 'U':case 'V':case 'W':case 'X':case 'Y':
          case 'Z':
                    resultis Capital

          case '0':case '1':case '2':case '3':case '4':
          case '5':case '6':case '7':case '8':case '9':
                    resultis Digit

          case '*s':case '*t':case 0:
                    resultis Ignorable

          default:  resultis Simple  $)  $)


