           EXTERNAL FUNCTION
           NORMAL MODE IS INTEGER
           PROGRAM COMMON LIST(84),MAX,FROM(20),
          1TO(20),FSUB(20),TSUB(20),USES(60),CURCOR
           VECTOR VALUES STOSTA=0601K8
           VECTOR VALUES MTTR=002101K6
           VECTOR VALUES MTSX=0074014K5
           VECTOR VALUES TSX=0074004K5
           VECTOR VALUES TXL=700001K6
          R
          RINSERT BREAK POINTS IN USER CORE
          R
           ENTRY TO B.
           FWRKBL.
           LOAD.
           ARGSL.
           MAP.($PROLOG$,PFROM,PCOUNT)
           PLAST=PFROM+PCOUNT-1
           MDBG=ENTRY.($(MDBG)$)
           BRKREG.(FIRST,COUNT)
           PATCH=FIRST+3
           FIRLOC=SNLOC.(1)
          R
           THROUGH BREAK, FOR I=1,1,I.G.MAX
           THROUGH BREAK, FOR AT=FROM(I),1,AT.G.TO(I)
           ABSLOC=SNLOC.(AT)
           WHENEVER FIRLOC.E.ABSLOC.AND.PCOUNT.E.0,ABSLOC=ABSLOC+1
           INST=INS.(ABSLOC)
           WHENEVER INST.A.7777777K5.E.MTSX,TRANSFER TO BREAK
          R
           THROUGH PSPACE,FOR PATCH=PATCH,2,PATCH.G.FIRST+COUNT-1
PSPACE     WHENEVER BRK.(PATCH).E.0,TRANSFER TO GOTPAT
           LIST=0
           ADDCOM.(COM1)
           VECTOR VALUES COM1=49,$PATCH SPACE EXAUSTED.  R$,
          1$EQUEST TERMINATED BEFORE $
           ADDSL.(AT)
           CRTYPE.
           TRANSFER TO PROLOG
          R
GOTPAT     SETINS.(ABSLOC,MTSX.V.PATCH)
           SETBRK.(PATCH,TXL.V.MDBG.V.1K6)
           WHENEVER INST.A.7777777K5.E.TSX,INST=INST.A.77777K.V.MTTR
           SETBRK.(PATCH+1,INST)
          R
BREAK      CONTINUE
          R
PROLOG     THROUGH SET,FOR J=PFROM,1,J.G.PLAST
           PINST=INS.(J)
           WHENEVER PINST.A.7757777K5.E.STOSTA
           WHENEVER PINST.A.77777K.L.PFROM
           INST=INS.(PINST.A.77777K)
           WHENEVER INST.A.7777777K5.E.MTSX,SETINS.
          1(J,PINST.A.7777777K5.V.((INST.A.77777K)+1))
           END OF CONDITIONAL
SET        END OF CONDITIONAL
          R
           READY.
          R
          RKILL BREAK POINTS IN USER PROG
          R
           ENTRY TO K.
           FWRKBL.
           LOAD.
           ARGSL.
           MAP.($PROLOG$,PFROM,PCOUNT)
           FIRLOC=SNLOC.(1)
          R
           THROUGH KILL,FOR I=1,1,I.G.MAX
           THROUGH KILL, FOR AT=FROM(I),1,AT.G.TO(I)
           ABSLOC=SNLOC.(AT)
           WHENEVER FIRLOC.E.ABSLOC.AND.PCOUNT.E.0,ABSLOC=ABSLOC+1
           INST=INS.(ABSLOC)
           WHENEVER INST.A.7777777K5.NE.MTSX,TRANSFER TO KILL
          R
           PATCH=INST.A.77777K
           INST=BRK.(PATCH+1)
           WHENEVER INST.A.7777777K5.E.MTTR,INST=INST.A.77777K.V.TSX
           SETINS.(ABSLOC,INST)
           SETBRK.(PATCH,0)
           SETBRK.(PATCH+1,0)
          R
           FIND=STOSTA.V.(PATCH+1)
           THROUGH RESET, FOR TRY=0,1,TRY.E.PCOUNT
           PINST=INS.(PFROM+TRY)
           WHENEVER PINST.A.775777777777K.E.FIND
           SETINS.(PFROM+TRY,PINST.A.7777777K5.V.ABSLOC)
           TRANSFER TO KILL
RESET      END OF CONDITIONAL
          R
KILL       CONTINUE
           READY.
           END OF FUNCTION
