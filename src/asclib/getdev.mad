          R..  ROUTINE TO GET DEVICE NUMBER OF TYPEWRITER CONSOLE.
          R..  J. H. SALTZER, DECEMBER, 1966.
          R
           EXTERNAL FUNCTION(DEVICE, PLTH)
           NORMAL MODE IS INTEGER
           VECTOR VALUES M10LTH = 130
           VECTOR VALUES M27LTH = 120
           VECTOR VALUES M35LTH = 84
           VECTOR VALUES M37LTH = 84
           VECTOR VALUES M37 = 1
           VECTOR VALUES M1050C = 2
           VECTOR VALUES M2741E = 3
           VECTOR VALUES M35 = 4
           DIMENSION GLOP(4)
          R
           ENTRY TO GETDEV.
           WHOAMI.(GLOP(4)...4)
           JUNK = GLOP(1) .RS. 30
           WHENEVER JUNK .E. 1 .OR. JUNK .E. 4 .OR. JUNK .E. 6
              DEVICE = M35
              PLTH = M35LTH
           OR WHENEVER JUNK .E. 2
              DEVICE = M2741E
              PLTH = M10LTH
           OR WHENEVER JUNK .E. 8
              PLTH = M27LTH
              DEVICE = M2741E
           OR WHENEVER JUNK .E. 7
              DEVICE = M37
              PLTH = M37LTH
           OTHERWISE
              DEVICE = M37
              PLTH = 80
           END OF CONDITIONAL
           FUNCTION RETURN
           END OF FUNCTION
