* 
       TTL     STAND-ALONE VERSION FOR RETRIEVAL .SETUP                 
       LBL     S.STUP,X
       TITLE
       ENTRY   .SETUP   
       REM
       EXTERN  SETIO,CDATE,GETIME,EXIT,UPDMFD,ATTACH,GETARG 
       EXTERN  CLOSE,UMOUNT   
       REM
       REM     THIS ROUTINE IS CALLED IMPLICITLY BY (MAIN). 
       REM     IF FUNCTIONS ROUGHLY THE SAME AS 'B.STUP', THE     
       REM     CTSS-COMPATIBLE BACKGROUND VERSION.  WITH THIS     
       REM     VERSION, TRANSFERRING TO 34(8) PROVIDES THE  
       REM     EMERGENCY CLOSE PROCEDURE. 
       REM
       REM                   D.R. WIDRIG  DEC. 1965   
       REM
.SETUP SXA     IR4,4         SAVE LINKAGE 
       HTR     *+1           DEBUGGING HALT     
       TSX     SETIO,4       INITIALIZE FILE SYSTEM   
       PZE     =99999        SET DAEMON AS AUTHOR     
       TSX     GETIME,4      GET CURRENT DATE   
       STQ     DATE          SAVE IT
       TSX     CDATE,4       GIVE DATE TO CONVERSION PACKAGE
       PTH     DATE          ..     
       CLA     ECLOSE        GET EMERGENCY CLOSE
       STO     28            SAVE IN 34(8)
       TSX     UPDMFD,4      INSURE DAEMON IN M.F.D.  
       PTH     SYSTEM        ..     
       PTH     DAEMON        ..     
       PTH     *+1           IGNORE ERROR RETURN
       TSX     ATTACH,4      MAKE SURE ATTACHED TO DAEMON   
       PTH     SYSTEM        ..     
       PTH     DAEMON        ..     
       TSX     GETARG,4      PRIME INPUT LINE   
IR4    AXT     **,4          RESTORE RETURN     
       TRA     1,4           RETURN TO MAIN PROGRAM   
       EJECT
ECLOSE TTR     *+1           TRANSFER TO HERE ON EMERGENCY  
       TSX     CLOSE,4       CLOSE ALL FILES    
       PTH     =HALL         ..     
       PTH     =-0           PLACE HOLDER 
       PTH     *+1           IGNORE ANY ERRORS  
       TSX     UMOUNT,4      UNMOUNT ANY TAPES LEFT   
       PTH     =HALL         ..     
       PTH     UMTMS,,4      SIMPLE MESSAGE     
       PTH     *+1           NO ERRORS, PLEASE  
       TRA     EXIT     
       EJECT
       REM     CONSTANTS, ETC.
DATE   PZE  
SYSTEM BCI     1, S0056 
DAEMON BCI     1, 99999 
UMTMS  BCI     4,PANIC CLOSE AND UNMOUNT  
       DETAIL
       END  
