           EXTERNAL FUNCTION (N,C1,C2,C3)
           NORMAL MODE IS INTEGER
           BOOLEAN CRP
           VECTOR VALUES SAVE=$  SAVE(MDBG)     P$,777777777777K
           VECTOR VALUES RESUME=$RESUME(MDBG)$,777777777777K
           VECTOR VALUES CHMODE=$CHMODE(MDBG) SAVED     0$,777777777777K
          R
          REXECUTE N.LE.2 CTSS COMMANDS
          R
           ENTRY TO XEQ.
           WHENEVER N.LE.0.OR.N.G.3
           PRINT.(COM1)
           VECTOR VALUES COM1=27,$XEQ ERROR, CONSULT LISTING.$
           READY.
           END OF CONDITIONAL
           SCLS.(SAVE,1)
           SCLS.(C1,2)
           WHENEVER N.G.1,SCLS.(C2,3)
           WHENEVER N.G.2,SCLS.(C3,4)
          R
           WHENEVER SAVE(2).E.$     T$
           SCLS.(RESUME,N+2)
           SCLC.(1,N+2)
           OR WHENEVER N.L.3
           SCLS.(CHMODE,N+2)
           SCLS.(RESUME,N+3)
           SCLC.(1,N+3)
           OTHERWISE
           SCLS.(RESUME,5)
           SCLC.(1,5)
           END OF CONDITIONAL
          R
           CHNCOM.(1)
           WHENEVER N.E.3.AND.SAVE(2).E.$     P$,DELETE.(CHMODE(1))
           FUNCTION RETURN
          R
          RSET THE MODE OF FILE (MDBG) SAVED TO 'T' OR 'P'
          R
           ENTRY TO MODE.
           SPACE.
           GETBCD.(BCD)
           SPACE.
           WHENEVER BCD.NE.$T$.AND.BCD.NE.$P$.OR..NOT.CRP.(0)
           PRINT.(COM1)
           VECTOR VALUES COM1=27,$TRY AGAIN WITH 'T' OR 'P'.$
           READY.
           END OF CONDITIONAL
           RTJUST.(BCD)
           SAVE(2)=BCD
           READY.
           END OF FUNCTION
