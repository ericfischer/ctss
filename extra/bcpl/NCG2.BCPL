// NCG2.BCPL 05/23/68 12:52

get 'NHEAD3'

let Cgfap(N) be
      $(1 let v = vec 200
          ConstV, ConstP, ConstT := v, 0, 200
       $( let v = vec 200
          ConstL := v
       $( let v = vec 300
          Svec, SvecP, SvecT := v, 0, 300
       $( let v = vec 300
          StatV, StatP, StatT := v, 0, 300
       $( let v = vec 500
          TempV, TempT := v, v+500
       $( let v = vec 2000
          Parv, Paramnumber := v, 1000
       $( let v = vec 2000
          RefV, RefP := v, 0
       $( let v = vec 4000
          STV := v

          Nulladdr := table 0, 0, 0, 0, 0, 0
          Glob0 := table 0, 0, 0, 1, 0, 0
          V1 := table 0, 0, 0, 0, 0, 0
          V2 := table 0, 0, 0, 0, 0, 0

          Commonbreak := $876640
          Initstack(0)

          SetupBSS(N)
          if Filing & N ne 0 do OUTPUT := Createoutput(N, BCDword('  LIST'), 0)
          Endlab := Nextparam()
          Scan()
          Outconsts()
          Outnumbs()
          WRLAB(Endlab)
          Compile(PCA, Nulladdr)
          if Filing & N ne 0 do Endwrite(OUTPUT)
          OUTPUT := MONITOR
          if N=0 do N := BCDword('.BCPL.')
          if BSSing do WRBSS(N)  $)1

and Nextparam() = valof
          $( Paramnumber := Paramnumber + 1
             resultis Paramnumber  $)

and Initstack(S) be
       $( Arg2, Arg1 := TempV, TempV + Tempsize
          SSP := S
          H1*(Arg2), H2*(Arg2), H3*(Arg2), H5*(Arg2) := LOCAL, 0, SSP-2, SSP-2
          H1*(Arg1), H2*(Arg1), H3*(Arg1), H5*(Arg1) := LOCAL, 0, SSP-1, SSP-1  $)
