* M1416 3845   PETER R BOS  10/30/68  .... FILE SYSTEM ERROR DIAGNOSTIC 
       PCC     ON 
       NOLNK
       ENTRY   PRDIAG   
       LBL     PRDIAG   
       PCC     OFF
       TITLE
       UNLIST                NO LISTING OF MACRO PACKAGE    
CALL1  MACRO   SUBR
       TSX     SUBR,4        GENERATE SYSTEM INSTRUCTION.
CALL1  END
CALL2  MACRON  LIST
       IRP     LIST
       PAR     LIST
       IRP
CALL2  END
CALL   MACRO   SUBR,LIST
       CALL1   SUBR
       CALL2   (LIST)
       RMT
       PMC     ON
       IFF     SUBR,0,1      IF ARGUMENT IS UNDEFINED,
SUBR   TIA     =H'SUBR
       PMC     OFF
       RMT
CALL   END
       REM
       REM     SAVE, RESTORE MACROS ....                              
       REM                                                            
SAVE   MACRO   ID,LIST       SAVE BASIC MACHINE CONDITIONS            
       PMC     OFF           .. NO PRINTING                           
       IRP     LIST          .. E.G. 'AC', 'X1'                       
       SAV1    (ID),LIST,,   CONTEXT FREE MACROS                      
       IRP                   ..                                       
SAVE   END                   ..                                       
       REM                                                            
RESTOR MACRON  ID,LIST,FLG   RESTORE MACRO FOR BASIC MACH. COND.      
       PMC     OFF           ..                                       
       IRP     LIST          USED IN SAME MANNER AS SAVE              
       IFF     1,*,FLG       IF '*' SPECIFIED,                        
       RST1    (ID),0,LIST,, ASSEMBLES 'AXT' FOR 'X1' ... 'X7'        
       IFF     0,*,FLG       IF NO '*' SPECIFIED,                     
       RST1    (ID),,LIST,,  ASSEMBLES 'LXA' FOR 'X1'...'X7'          
       IRP                   ..                                       
RESTOR END                   ..                                       
       REM                                                            
RETURN MACRO   LOC,ID,LIST   RESTORE WITH OPTIONAL RETURN             
       PMC     OFF           .. DON'T PRINT EXPANSION                 
       IRP     LIST          .. ASSEMBLES 'AXT' FOR X1 ... X7,        
       RST1    (ID),0,LIST,, .. DEFINING DESIRED SYMBOL               
       IRP                   ..                                       
       IFF     0,,LOC        IF RETURN LOCATION GIVEN,                
       TRA     LOC           USE AS TRANSFER ADDRESS (E.G. (1,4) )    
RETURN END                   ..                                       
       REM                                                            
SAV1   MACRO   ID,REG,LOC    MACRO TO PRODUCE CODE TO SAVE ITEM       
       IFF     1,,LOC        IF LOCATION NOT SPECIFIED,               
       SV'REG  ID'REG        USE NAME OF ITEM WITH PREFIX             
       IFF     0,,LOC        IF LOCATION SPECIFIED,                   
       SV'REG  LOC           USE SPECIFIED LOCATION                   
SAV1   END                   ..                                       
       REM                                                            
RST1   MACRO   ID,FLAG,REG,LOC MACRO TO RESTORE ITEM                  
       IFF     1,,LOC        IF LOCATION NOT SPECIFIED                
       LD'REG  ID'REG,FLAG,  USE REGISTER NAME AND PREFIX             
       IFF     0,,LOC        IF SPECIFIED,                            
       LD'REG  LOC,FLAG,     USE IT                                   
RST1   END                   ..                                       
       REM                                                            
       LIST
       REM
PRDIAG SXA     PRX4,4        SAVE LINKAGE 
       CALL    $IODIAG((TMP,,6)) GET FILE SYSTEM DIAGNOSTIC 
       CLA     TMP+4         FILE NAME    
       LDQ     TMP+5         ..     
       DST     DIAG+10       ..     
       CAL     TMP+1         NAME OF FCOR ENTRY 
       SLW     DIAG+8        ..     
       CALL    $OCRBC(TMP)   LOCATION OF CALL   
       ORA     =O570000000000 ..    
       SLW     DIAG+5        ..     
       LDQ     I.O           'I/O ' 
       CAL     TMP+3         I/O ERROR CODE IF ANY    
       TNZ     *+3           IF ZERO, ORDINARY ERROR  
       LDQ     =O575757575757 ..    
       CAL     TMP+2         ERROR CODE.  
       STQ     DIAG          ..     
       SLW     TMP           CONVERT ERROR CODE 
       CALL    $DERBC(TMP)   ..     
       ORA     =O575760000000 ..    
       SLW     DIAG+2        ..     
       REM
       CALL    WRFLX((DIAG,,12)) PRINT DIAGNOSTIC     
PRX4   AXT     -,4           RESTORE LINKAGE    
       TRA     1,4           RETURN TO CALLER   
       DETAIL
       REM
       RMT     *
       REM
       REM
       REM     ..... STORAGE AND CONSTANTS ......     
       REM
DIAG   VFD     36/0,H30/ERROR,O6/57,36/0,O6/60, 
       ETC     H30/FOUND,O6/60,H12/AT,O18/605757,     
       ETC     36/0,O6/60,H12/IN,O6/60,H24/CALL,O6/60,
       ETC     H12/TO,O6/60,36/0,O6/60,H18/FOR, 
       ETC     O12/6057,36/0,36/0   
I.O    VFD     H18/I/O,O18/605757   
TMP    BSS     6             TEMPORARY ARRAY    
       END  
