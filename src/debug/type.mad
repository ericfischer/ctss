           EXTERNAL FUNCTION (A1,A2)
           NORMAL MODE IS INTEGER
           STATEMENT LABEL A2
           PROGRAM COMMON LIST(84),MAX,FROM(20),TO(20),
          1FSUB(20),TSUB(20),USES(60),CURCOR,PATCH,SKIP
           DIMENSION BUFF(14)
           VECTOR VALUES CR=556060606060K
           VECTOR VALUES NULL=576060606060K
           VECTOR VALUES INTEST=0B
          R
          RADD A CARRIAGE RETURN TO LIST AND PRINT IT.
          R
           ENTRY TO CRTYPE.
           LIST=LIST+1
           LIST(LIST)=CR
          R
          RJUST PRINT LIST
          R
           ENTRY TO TYPE.
           WHENEVER LIST.G.84
           PRINT.(COM1)
           VECTOR VALUES COM1=61,$MADBUG IS TRYING TO PRINT TOO$,
          1$LONG A LINE.  CONSULT LISTINGS.$
           LIST=84
           LIST(LIST)=CR
           END OF CONDITIONAL
          R
           THROUGH BLANK,FOR LIST=LIST,1,LIST.E.LIST/6*6
BLANK      LIST(LIST+1)=NULL
           BUFF=LIST
           LIST=1
           WCOUNT=BUFF/6
           THROUGH INSERT, FOR I=1,1,I.G.WCOUNT
           BCD=0
           THROUGH WORD, FOR J=0,6,J.E.36
           BCD=(LIST(LIST).A.77K10).RS.J.V.BCD
WORD       LIST=LIST+1
INSERT     BUFF(I)=BCD
           PRINTA.(BUFF)
           FUNCTION RETURN
          R
          RCALL SHORT FAST PROGRAM TO PROCESS INPUT LINES
          R
           ENTRY TO DOLINE.
           WSEEK.($DOLINE$)
           WSET.(SKIP+1+24,A1.V.WGET.(SKIP+1+24).A.7777777K5)
           SIZE=WGET.(1).A.77777K
           WDUMP.($DOLINE$)
           CHMODE.(DOLSAV,0)
           XEQ.(2,RESUME,SAVE)
           VECTOR VALUES RESUME=$RESUME(MBGI)$
           VECTOR VALUES SAVE=$  SAVE(MBGI)$
           VECTOR VALUES DOLSAV=$(MBGI) SAVED$
           CHMODE.(DOLSAV,0)
           WSETUP.($INPUT$,DOLSAV,0)
           WSIZE.(TSIZE)
           WHENEVER TSIZE.E.0
           WDITCH.($INPUT$)
           READY.
           END OF CONDITIONAL
           DELTA=WGET.(1).A.77777K-SIZE
           WDEL.(1,SIZE+SKIP)
           WSIZE.(SIZE)
           WDEL.(DELTA+1,SIZE-DELTA)
           INTEST=1B
           FUNCTION RETURN
          R
          RGET AN INPUT LINE READ IN BY SHORT FAST PROGRAM
          RTRANSFER TO A2 IF END OF INPUT LINE BLOCK
          R
           ENTRY TO INLINE.
           WSEEK.($INPUT$)
           WHENEVER WGET.(1).E.777777777777K
           WDEL.(1,1)
           TRANSFER TO A2
           END OF CONDITIONAL
           THROUGH IN,FOR I=1,1,I.G.12
IN         A1(I)=WGET.(I)
           WDEL.(1,12)
           FUNCTION RETURN
          R
          RREAD A LINE INTO LIST
          R
           ENTRY TO READ.
           WHENEVER INTEST
           WSEEK.($INPUT$)
           INTEST=0B
           WSIZE.(SIZE)
           WHENEVER SIZE.L.15
           WDITCH.($INPUT$)
           PRINT.(COM2)
           VECTOR VALUES COM2=32,$DOLINE ERROR.  CONSULT L$,
          1$ISTINGS.$
           READY.
           END OF CONDITIONAL
           WHENEVER SIZE.G.15
           WDEL.(1,SIZE-15)
           PRINT.(COM2)
           END OF CONDITIONAL
           THROUGH RIN,FOR I=0,1,I.G.14
RIN        BUFF(I)=WGET.(I+1)
           WDEL.(1,15)
           WDITCH.($INPUT$)
           OTHERWISE
           READIN.(BUFF)
           END OF CONDITIONAL
           LIST=1
           COUNT=1
           THROUGH GET,FOR I=1,1,0B
           BCD=BUFF(I)
           THROUGH GET, FOR J=0,6,J.E.36
           LIST(COUNT)=(BCD.LS.J).A.77K10.V.$0$
           COUNT=COUNT+1
           WHENEVER COUNT.GE.BUFF
           LIST(COUNT)=CR
           THROUGH BLANK1,FOR I=COUNT+1,1,I.G.84
BLANK1     LIST(I)=$ $
           FUNCTION RETURN
GET        END OF CONDITIONAL
           END OF FUNCTION
