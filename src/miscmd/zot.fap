* ZOT - NEW GAZABE COMPLEX
       TTL    OCTOBER 19, 1968  LHM
*
*
       ABS
*
       ORG,0
*
*  UNIVERSAL EXIT. MUST BE LOADED AT LOCATION ZERO.
*
EXIT   TSX,WRFLXA,4
ZERO
       TIA,CHAIN
*
*  RANDOM CONSTANTS
*
COLONS OCT,350000350000
NULLS  OCT,005700570057
CRWORD OCT,555757575757
       EVEN
THEWRD OCT,575757575757
SPARE  BCI,1,******
CLCWRD PZE,,,5
TIME   PZE,60
BLANK  OCT,60
SLASH  OCT,61
*
*  REWRITE THE COMMAND BUFFER FOR NEXCOM.
*
FIN    STD,*+2
COMSTO SLW,WRMESS-XR1,1
       TIX,COM,1,1
       TSX,SETCLS,4
SETLOC PZE,WRMESS,,0
       CAL,WRMESS
       LDQ,WRMESS+1
NEXLOC TSX,NEXCOM,4
*
*  MAIN LOOP. FIND THE COMMAND.
*   NOTE THAT THIS MUST BEGIN AT LOC (24)8
*
LOOP   AXT,1,1
       ZSD,CONV
GETARG TSX,NUM,2
       TIX,BUMP,2,XR2-GETARG
*
*  THE ARGUMENT WAS A NUMBER. REPEAT THE LAST CHARACTER TYPED.
*
       PAX,,1
       TRA,WRTIX
*
*  REGULAR ARGUMENT. GO FIND IT IN THE TABLE.
*
XR2    EQU,*-1
       LAS,TABTOP-XR2,2
       TIX,*-1,2,2
       TRA*,TABTOP+1-XR2,2
       AXC,FIN,2
       SCA,NOCOM,2
XR1    TSX,COMSTO,1
*
*  PICK UP THE NEXT COMMAND ARGUMENT.
*
NUM    LDI,BLANK
       TRA,*+2
COM    LDI,ZERO
COMCAL TSX,GETCOM,4
COMNO
       SSP
       TIF,*+2
       TRA,1,2
       RFT,60
       TXI,CONV,2,-1
BUMP   LXA,COMNO,4
       TXI,*+1,4,1
       SXA,COMNO,4
       LAS,TABBOT
       ZSA,COMCAL
NOCOM  TSX,EXIT,4
       TRA,1,2
*
*  BCD TO BINARY CONVERSION.
*
CONV   TXL,DTO,1,**
DTB    XCL
       STZ,TEMP
       AXT,6,1
       VLM,TABLE,1,4
       LGR,2
       ACL,TEMP
       SLW,TEMP
       TIX,*-4,1,1
       TRA,BUMP
*
*  BCD TO OCTAL CONVERSION.
*
DTO    LGR,3
       ARS,3
       TIX,DTO,4,32767/6
       LGL,18
       TRA,BUMP
*
*  PRINT A SINGLE CHARACTER.
*
PRINT  TSX,SETFUL,4
       CAL,NULLS
       SLW,WRMESS
       CAL,TABTOP+1-XR2,2
       STD,WRMESS
       AXT,1,2
SETCNT SXD,COUNT,2
WR     TSX,WRFLX,4
COUNT  PZE,WRMESS,,1
WRTIX  TIX,WR,1,1
       TPL,LOOP
DEATH  TIA,DEAD
*
*  FIDDLE WITH COMMAND BUFFERS.
*
GAZABE TSX,NUM,2
       CAL,TEN
       XCL
       VLM,SIXTY,,12
       TRA,ZOTIN
*
ZOT    TSX,NUM,2
       CAL,TIME
ZOTIN  SLW,TIME
       TSX,NUM,2
       PXA,,1
       ORA,CLCWRD
       TSX,SETCLC,4
       CAL,TIME
BYEBYE TZE,LOOP
       AXC,LOOP-1,4
       TIA,SLEEP
*
WAIT   TSX,NUM,2
       CAL,TIME
       SLW,TIME
       TRA,BYEBYE
*
CHAINX TSX,NUM,2
       PXA,,1
       ORA,CLCWRD
       TSX,SETCLC,4
       TRA,LOOP
*
*  PRINT THE NEXT WORD FROM THE COMMAND LINE.
*
PRWORD TSX,COM,2
PWRDIN AXT,1,2
       SLW,WRMESS
SETWR  AXC,SETCNT-1,4
GETBCD TIA,SETBCD
*
*  LOAD UP A GIVEN COMMAND BUFFER.
*
SETCOM ZSA,NEXLOC
       TSX,NUM,2
       PXA,,1
       PAX,,2
       SXD,SETLOC,2
NEXCM  AXC,FIN,2
       SCA,NOCOM,2
       TXI,COM,1,-XR1-1
*
*  REPEAT A FEW ARGUMENTS.
*
TIX    TSX,NUM,2
       ZAC
       SLW,TEMP2
       TSX,NUM,2
       TRA,NEXT
       NZT,REPEAT
       SLW,REPEAT
       LXA,REPEAT,4
       TNX,WIPE,4,1
       SXA,REPEAT,4
NEXT   CAL,TEMP2
       SLW,COMNO
       TRA,LOOP
*
WIPE   STZ,REPEAT
       TRA,LOOP
*
*  CONVERT TO OCTAL AND PRINT.
*
OCT    TSX,NUM,2
       TIX,BUMP,2,1
PNUMO  LGR,18
       ALS,3
       LGL,3
       TIX,*-2,2,32767/6
       TRA,PWRDIN
*
*  CONVERT TO DECIMAL AND PRINT.
*
DEC    SXD,CONV,4
       TSX,NUM,2
       TIX,BUMP,2,1
PNUM   VDP,TABLE,1,6
       ARS,6
       TIX,OUT,1,5
       TXI,*-3,1,1
*
OUT    XCA
       TRA,PWRDIN
*
*  PICK UP A COMMAND BUFFER AND PRINT IT ON LINE.
*
GETCL  TSX,NUM,2
GETIN  PXA,,1
       PAX,,4
       SXD,GETBUF,4
       TSX,GETCLS,4
GETBUF PZE,WRMESS,,**
       AXT,20,1
       LDQ,THEWRD
GLOOP  CAL,WREND,1
       LAS,TABBOT
       STQ,WREND,1
GTIX   TIX,*-1,1,1
       TIX,GLOOP,1,1
       TSX,GETBCD,4
       TSX,WRFLX,4
       PZE,WRMESS,,10
       TSX,WRFLX,4
       PZE,WRMESS+10,,10
GOUT   TRA,LOOP
*
*  SWITCH BETWEEN WRFLX AND WRFLXA.
*
SWCHCR LXA,EXIT,4
       LXA,WR,2
       SXA,EXIT,2
       SXA,WR,4
       TRA,LOOP
*
*  CONDITIONALLY PRINT A CARRIAGE RETURN.
*
IFFCR  PXA,,1
       SUB,REPEAT
       TZE,LOOP
       TXI,PRINT,2,QIFFCR-CR
*
*  EJECT A PAGE.
*
EJECT  CAL,CRWORD
       TXI,PWRDIN,1,32
*
*  DO VARIOUS ARITHMETIC FUNCTIONS.
*
NOREC  TXI,ARITH,1,431
*
ASOCT  SXD,CONV,4
ADDSUB TXI,ARITH,1,-1
*
AROCT  SXD,CONV,4
ARITH  STZ,TEMP2
       SXA,TEMP2,1
       SXD,SWITCH,1
       AXT,1,1
       LDQ,TABTOP+1-XR2,2
       SLQ,INST
ALOOP  TSX,NUM,2
       TRA,PICK
SWITCH TXL,MQ,1,**
INST   ***,TEMP2
STORE  SLW,TEMP2
       TRA,ALOOP
*
MQ     LRS,35
       XEC,INST
       STQ,TEMP2
       TZE,ALOOP
       PXA,,1
       ADD,TEMP2
       TRA,STORE
*
*
PICK   CAL,TEMP2
       LXD,CONV,4
       TXH,PNUMO,4,0
       TRA,PNUM
*
*  REPEAT THE CURRENT COMMAND BUFFER
*
ECHO   AXT,NEXCM,4
       SXA,GOUT,4
       TXI,GETIN,1,-1
*
*  SWITCH BETWEEN LONG AND SHORT FORM FOR GETCLS.
*
CHGCLS CLA,SPARE
       LDQ,THEWRD
       DST,THEWRD
       LXD,GTIX,4
       SCD,GTIX,4
       TRA,LOOP
*
*  LEFT OR RIGHT JUSTIFY A FILE NAME.
*
LEFT   TXI,*+1,1,1
RIGHT  TXI,*+1,1,2
GETNAM TSX,COM,2
       SLW,NAMES,1
       SIL,770000
       ERA,BLANKS
       TXI,JLOOP,2,GETNAM+6
       ALS,6
       TNX,JOUT,2,1
JLOOP  TIF,*-2
JOUT   ERA,BLANKS
       XCL
       TIX,GETNAM,1,2
       TXH,*+2,1,1
       TXI,*+1,1,2
       SLW,NAMES-1,1
       STQ,NAMES+1,1
CALCHG TSX,CHFILE,4
       BRN,OLD1
       BRN,OLD2
       BRN,ZERO
       BRN,NEW1
       BRN,NEW2
       BRN,*+2
       TRA,LOOP
*
       CAL,NOMESS
       TXI,PWRDIN,1,-2
*
*  PRINT OUT THE DATE AND TIME.
*
MAKTIM TSX,GETIME,4
       SLW,TEMP
       PIA
       LGL,12
       ALS,6
       ORA,SLASH
       LGL,12
       SLW,WRMESS
       CAL,SLASH
       LGL,18
       ORA,BLANK
       LGL,12
       PAI
       LDQ,TEMP
       ZAC
       VDP,Q1,,29
       VDP,Q2,,6
       XCL
       OAI
       STI,WRMESS+1
       ZAC
       VDP,Q3,,30
       VDP,Q4,,6
       VDP,Q5,,12
       VDP,Q6,,6
       XCL
       ORA,COLONS
       SLW,WRMESS+2
       TIX,SETWR,2,TABTOP-XR2-QTIME-3
*
*  PRINT OUT THE NEXT CHARACTER (EXPRESSED IN OCTAL).
*
CHAR   TSX,COM,2
       LDQ,ZERO
       LGR,3
       ARS,3
       TIX,*-2,2,32767/4
       SLQ,TEMP
       TXI,PRINT,2,TABTOP+1-XR2-TEMP+CHAR+32767/4*3
*
*
*
*  CONSTANTS AND STORAGE
*
WRFLXA TIA,WRFXA
GETCOM TIA,GETC
SETCLS TIA,SETS
CHFILE TIA,CHGFIL
NEXCOM TIA,NEXC
SETFUL TIA,SETF
SETCLC TIA,SETC
WRFLX  TIA,WRFX
GETCLS TIA,QGET
GETIME TIA,QGETTM
*
CHAIN  BCI,1,CHNCOM
WRFXA  BCI,1,WRFLXA
WRFX   BCI,1,WRFLX
GETC   BCI,1,GETCOM
SETF   BCI,1,SETFUL
SETC   BCI,1,SETCLC
DEAD   BCI,1,DEAD
SLEEP  BCI,1,SLEEP
SETBCD BCI,1,SETBCD
CHGFIL BCI,1,CHFILE
QGETTM BCI,1,GETIME
*
       DEC,1E0B29,1E1B29,1E2B29,1E3B29,1E4B29,1E5B29
TABLE  EQU,*
*
WRMESS OCT,575757575757
WREND  BES,19
TEMP   EQU,WREND-2
TEMP2  EQU,WREND-1
*
OLD1   EQU,WREND-6
NEW1   EQU,WREND-5
OLD2   EQU,WREND-4
NEW2   EQU,WREND-3
NAMES  EQU,NEW2+1
*
TEN    PZE,10
SIXTY  DEC,60B23
REPEAT
BLANKS BCI,1,
NOMESS BCI,1,NOFILE
*
Q1     DEC,216E4B41
Q2     DEC,216E3B35
Q3     DEC,36E3B40
Q4     DEC,36E2B34
Q5     DEC,60E1B22
Q6     DEC,60E0B16
*
       EJECT
*
*  DISPATCH TABLE
*
TABTOP EQU,*
       BCI,1,ADDOCT
       ADD,ASOCT
       BCI,1,CHGCLS
       PZE,CHGCLS
       BCI,1,DIVOCT
       DVP,AROCT
       BCI,1,GAZABE
       PZE,GAZABE
QGET   BCI,1,GETCLS
       PZE,GETCL
       BCI,1,MULOCT
       MPY,AROCT
NEXC   BCI,1,NEXCOM
       PZE,NEXCM
       BCI,1,NORECS
       DVP,NOREC
       BCI,1, BLACK
       VFD,O12/0132,24/PRINT
       BCI,1, CHAIN
       PZE,CHAINX
QEJECT BCI,1, EJECT
       PZE,EJECT
QIFFCR BCI,1, IFFCR
       PZE,IFFCR
       BCI,1, PRINT
       PZE,PRWORD
       BCI,1, RIGHT
       PZE,RIGHT
       BCI,1,  BELL
       VFD,O12/0113,24/PRINT
       BCI,1,  CHAR
       PZE,CHAR
       BCI,1,  ECHO
       PZE,ECHO
       BCI,1,  LEFT
       PZE,LEFT
       BCI,1,  NULL
       VFD,O12/0057,24/PRINT
       BCI,1,  POFF
       VFD,O12/0117,24/PRINT
       BCI,1,   ADD
       ADD,ADDSUB
       BCI,1,   DEC
       PZE,DEC
       BCI,1,   DIE
       PZE,DEATH
       BCI,1,   DIV
       DVP,ARITH
       BCI,1,   EXC
       VFD,O12/0114,24/PRINT
       BCI,1,   MUL
       MPY,ARITH
       BCI,1,   OCT
       PZE,OCT
       BCI,1,   PMV
       PZE,TABBOT
       BCI,1,   PON
       VFD,O12/0175,24/PRINT
       BCI,1,   RED
       VFD,O12/0133,24/PRINT
       BCI,1,    BS
       VFD,O12/0135,24/PRINT
CR     BCI,1,    CR
       VFD,O12/0055,24/PRINT
       BCI,1,    LF
       VFD,O12/0107,24/PRINT
       BCI,1,     -
       VFD,O12/4116,24/PRINT
       BCI,1,    SP
       VFD,O12/0060,24/PRINT
       BCI,1,   SUB
       SUB,ADDSUB
       BCI,1,   TAB
       VFD,O12/0072,24/PRINT
       BCI,1,   TIX
       PZE,TIX
       BCI,1,   YES
       VFD,O12/0054,24/PRINT
       BCI,1,   ZOT
       PZE,ZOT
QTIME  BCI,1,  TIME
       PZE,MAKTIM
       BCI,1,  WAIT
       PZE,WAIT
       BCI,1, SPACE
       VFD,O12/0055,24/PRINT
       BCI,1, UNDER
       VFD,O12/0140,24/PRINT
SETS   BCI,1,SETCLS
       PZE,SETCOM
       BCI,1,SUBOCT
       SUB,ASOCT
       BCI,1,SWCHCR
       PZE,SWCHCR
TABBOT EQU,*
       OCT,777777777776
*
       END,LOOP
