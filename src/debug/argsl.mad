           EXTERNAL FUNCTION
           NORMAL MODE IS INTEGER
           PROGRAM COMMON LIST(84),MAX,FROM(20),TO(20)
           VECTOR VALUES CR=556060606060K
          R
          RCONVERTS STATEMENT LABELS GIVEN AS REQUEST
          RARGUMENTS TO CARD NUMBERS IN FROM AND TO.
          R
           ENTRY TO ARGSL.
           SPACE.
          R
           WHENEVER LIST(LIST).E.CR
           MAX=1
           FROM(1)=1
           LASCRD.(TO(1))
           FUNCTION RETURN
           END OF CONDITIONAL
          R
           THROUGH LOOP, FOR MAX=1,1,MAX.G.20
           GETSL.(FROM(MAX),ERROR)
           WHENEVER LIST(LIST).E.$.$.AND.LIST(LIST+1).E.$.$.AND.
          1LIST(LIST+2).E.$.$
           LIST=LIST+3
           GETSL.(TO(MAX),ERROR)
           OTHERWISE
           TO(MAX)=FROM(MAX)
           END OF CONDITIONAL
           SPACE.
LOOP       WHENEVER LIST(LIST).E.CR, TRANSFER TO CHECK
          R
           MAX=20
           PRINT.(COM1)
           VECTOR VALUES COM1=55,$ONLY 20 ARGUMENTS ALLOWE$,
          1$D.  THE REST HAVE BEEN IGNORED.$
          R
CHECK      THROUGH CKLOOP,FOR I=1,1,I.G.MAX
           WHENEVER TO(I).L.FROM(I)
           LIST=0
           ADDCOM.(COM2)
           VECTOR VALUES COM2=35,$INCORRECT BLOCK GIVEN FOR ARGUMENT $
           ADDNUM.(I)
           ADDCOM.(COM3)
           VECTOR VALUES COM3=39,$ WILL BE TREATED AS A SINGLE VARIABLE.
          1$
           CRTYPE.
           LIST=0
           ADDSL.(TO(I))
           ADDCOM.(COM4)
           VECTOR VALUES COM4=14,$ COMES BEFORE $
           ADDSL.(FROM(I))
           ADDCOM.(COM5)
           VECTOR VALUES COM5=22,$ IN THE SYMBOLIC DECK.$
           CRTYPE.
           TO(I)=FROM(I)
CKLOOP     END OF CONDITIONAL
          R
           FUNCTION RETURN
          R
ERROR      PRINT.(COM6)
           VECTOR VALUES COM6=10,$TRY AGAIN.$
           READY.
          R
           END OF FUNCTION
