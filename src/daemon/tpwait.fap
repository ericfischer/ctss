* TPWAIT - WAIT UNTIL I/O ON A OR B CHAN IS DONE. PROCESS ERRORS.           
*     
*      CALLING SEQUENCE -     
*              TSX           TPWAIT,4     
*              PTH           1 (FOR A,2 FOR B)  
*              TRA           EOFRTN E.O.F. RETURN     
*              TRA           ERRTN REDUN. CHECK RETURN
*              (NORMAL RETURN)
*     
*     
       LINK    OFF
       ENTRY   TPWAIT   
TPWAIT SXA     XR1,1         SAVE XRS     
       CAL     1,4           GET CHANNO   
       PAC     ,1            -CHANNO INTO XR 1  
       TXL     ERR,1,-3      BAD IF NOT A OR B  
BACK   XEC     TCOAB,1       WAIT UNTIL I/O IS DONE   
       XEC     TRCAB,1       TEST FOR REDUN. CHECKS   
       XEC     TEFAB,1       TEST FOR E.O.F.'S  
       TXI     *+1,4,-4      NORMAL RETURN IN 4,4     
XR1    AXT     **,1          RESTORE XR1  
       TRA     0,4           AND GO HOME  
       REM
TCOAB  SYN     *-1           WAIT UNTIL I/O IS COMPELED     
       TCOA    BACK          ON A   
       TCOB    BACK          OR ON B
       REM
TRCAB  SYN     *-1           TEST FOR REDUNDANCY CHECKS     
       TRCA    ERROR         ON A   
       TRCB    ERROR         OR ON B
       REM
TEFAB  SYN     *-1           TEST FOR AN E.O.F. 
       TEFA    EOF           ON A   
       TEFB    EOF           OR ON B
       REM
ERROR  TXI     XR1,4,-2      REDUN CHECKS RETURNS TO 2,4    
EOF    TXI     XR1,4,-3      EOF RETURNS TO 3,4 
ERR    SYN     ERROR         BAD CHAN IS SAME AS REDUN CHECK
       END  
