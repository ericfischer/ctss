* 
       TTL     CTSS LIBRARY ENTRIES                                     
       LBL     TSSLIB,X
       ENTRY   ZEL
       ENTRY   BZEL     
       ENTRY   WRITE    
       ENTRY   DERBC    
       ENTRY   BCDEC    
       REM
       EXTERN  DTBC,BTDC,WRFLX
       REM
       REM     THIS SET OF ROUTINES SUPPLIES CONVERSION     
       REM     ROUTINES CONTAINED IN CTSS LIBRARIES BUT     
       REM     NOT FOUND IN FMS BACKGROUND LIBRARIES  
       REM
       REM                   D.R. WIDRIG DEC. 1965    
       REM
BCDEC  SXA     IR4,4         SAVE RETURN  
       LDQ*    1,4           GET WORD TO BE CONVERTED 
       CRQ     A1,,6         ZERO LEADING BLANKS
       TSX     DTBC,4        CONVERT BCD TO BINARY    
       TRA     RETURN        EXIT   
       REM
DERBC  SXA     IR4,4         SAVE RETURN  
       LDQ*    1,4           GET WORD TO BE CONVERTED 
       TSX     BTDC,4        CONVERT BINARY WORD TO BCD     
       TRA     RETURN        EXIT   
       REM
ZEL    LDQ*    1,4           GET WORD TO BE ADJUSTED  
       CRQ     A1,,6         DO TABLE CONVERT   
       XCA                   RESTORE ANSWER TO AC     
       TRA     2,4           RETURN 
       REM
BZEL   LDQ*    1,4           GET WORD TO BE ADJUSTED  
       CRQ     B1,,6         DO TABLE CONVERT   
       XCA                   RESTORE ANSWER TO AC     
       TRA     2,4           RETURN 
       REM
WRITE  SXA     IR4,4         SAVE LINKAGE 
       CLA     1,4           GET POINTER TO VECTOR    
       STA     *+5           SAVE IT
       CLA*    2,4           GET WORD COUNT     
       ALS     18            MOVE TO DECREMENT  
       STD     *+2           SAVE WORD COUNT    
       TSX     WRFLX,4       PRINT LINE   
       PZE     **,,**        ..     
       TRA     RETURN        EXIT   
       REM
RETURN LXA     IR4,4         RESTORE LINKAGE    
       TRA     2,4           RETURN TO CALLER   
       TTL     CONSTANTS, TABLES, ETC.    
       TITLE
IR4    PZE  
A1     DUP     1,48     
       VFD     6/*-A1,30/C1   
       PZE     A1 
       DUP     1,15     
       VFD     6/*-A1,30/C1   
       REM
B1     SIX     B1 
       DUP     1,63     
       VFD     6/*-B1,30/C1   
       REM
C1     DUP     1,64     
       VFD     6/*-C1,30/C1   
       END  
