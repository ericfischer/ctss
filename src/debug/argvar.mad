           EXTERNAL FUNCTION(MODE8)
           NORMAL MODE IS INTEGER
           BOOLEAN MODE8
           PROGRAM COMMON LIST(84),MAX,FROM(20),TO(20),FSUB(20),TSUB(20)
           VECTOR VALUES CR=556060606060K
          R
          RCONVERTS VARIABLES GIVEN
          RAS REQUEST ARGUMENTS
          R
           ENTRY TO ARGVAR.
           SPACE.
          R
           WHENEVER LIST(LIST).E.$/$
           LIST=LIST+1
           GETFMT.(8)
           SPACE.
           MODE8=1B
           OTHERWISE
           MODE8=0B
           END OF CONDITIONAL
          R
           WHENEVER LIST(LIST).E.CR
           FIRVAR.(FROM(1),FSUB(1))
           LASVAR.(TO(1),TSUB(1))
           MAX=1
           FUNCTION RETURN 1B
           END OF CONDITIONAL
          R
           THROUGH GET, FOR MAX=1,1,MAX.G.20
           GETVAR.(FROM(MAX),FSUB(MAX))
           WHENEVER LIST(LIST).E.$.$.AND.LIST(LIST+1).E.$.$.AND.
          1LIST(LIST+2).E.$.$
           LIST=LIST+3
           GETVAR.(TO(MAX),TSUB(MAX))
           OTHERWISE
           TO(MAX)=FROM(MAX)
           TSUB(MAX)=FSUB(MAX)
           END OF CONDITIONAL
           SPACE.
GET        WHENEVER LIST(LIST).E.CR,TRANSFER TO CHECK
          R
           MAX=20
           PRINT.(COM1)
           VECTOR VALUES COM1=55,$ONLY 20 ARGUMENTS ALLOWE$,
          1$D.  THE REST HAVE BEEN IGNORED.$
          R
CHECK      THROUGH CKLOOP, FOR I=1,1,I.G.MAX
           WHENEVER TO(I).L.FROM(I).OR.TO(I).E.FROM(I).AND.TSUB(I).L.FSU
          1B(I)
           LIST=0
           ADDCOM.(COM2)
           VECTOR VALUES COM2=35,$INCORRECT BLOCK GIVEN FOR ARGUMENT $
           ADDNUM.(I)
           ADDCOM.(COM3)
           VECTOR VALUES COM3=39,$ WILL BE TREATED AS A SINGLE VARIABLE.
          1$
           CRTYPE.
           LIST=0
           WHENEVER TO(I).L.FROM(I)
           ADDNAM.(TO(I))
           ADDCOM.(COM4)
           VECTOR VALUES COM4=14,$ COMES BEFORE $
           ADDNAM.(FROM(I))
           ADDCOM.(COM5)
           VECTOR VALUES COM5=21,$ IN ALPHABETIC ORDER.$
           OTHERWISE
           ADDVAR.(TO(I),TSUB(I))
           ADDCOM.(COM4)
           ADDVAR.(FROM(I),FSUB(I))
           ADDCOM.(COM6)
           VECTOR VALUES COM6=22,$ BY LINEAR SUBSCRIPTS.$
           END OF CONDITIONAL
           CRTYPE.
           TO(I)=FROM(I)
           TSUB(I)=FSUB(I)
CKLOOP     END OF CONDITIONAL
          R
           FUNCTION RETURN 0B
           END OF FUNCTION
