           EXTERNAL FUNCTION (NAME)
           NORMAL MODE IS INTEGER
           BOOLEAN WSEEK,WCHANG,WRKBL
           PROGRAM COMMON LIST(84),MAX,FROM(20),TO(20),FSUB(20),TSUB(20)
          1,USES(60)
           VECTOR VALUES WORK=$ $
          R
          RGET THE NAME OF SUBROUTINE BEING WORKED
          R
           ENTRY TO GWORKA.
           NAME=WORK
           FUNCTION RETURN
          R
          RGET NAME OF SUBROUTINE BEING WORKED
          RBUT CONSIDER IT AN ERROR IF USER HAS NOT SET WORK
          R
           ENTRY TO GWORK.
           WHENEVER WORK.NE.$ $
           NAME=WORK
           FUNCTION RETURN
           END OF CONDITIONAL
ERR1       PRINT.(COM1)
           VECTOR VALUES COM1=34,$GIVE A WORK REQUEST AND TRY AGAIN.$
           READY.
          R
          RSET THE NAME OF SUBROUTINE BEING WORKED
          R
           ENTRY TO SWORK.
          R
           WHENEVER WSEEK.($PROG$)
           WHENEVER WCHANG.(0)
           ONE=$    00$
           TWO=$01    $
           WSIZE.(END)
           END=END/14
           THROUGH SL,FOR I=1,1,I.G.END
           WSET.(I*14-1,ONE)
           WSET.(I*14,TWO)
           TWO=TWO+1K8
           WHENEVER TWO.A.77K8.NE.12K8,TRANSFER TO SL
           TWO=TWO+66K8
           WHENEVER TWO.A.77K10.NE.12K10,TRANSFER TO SL
           TWO=$00    $
           ONE=ONE-1
           WHENEVER ONE.A.77K.NE.12,TRANSFER TO SL
           ONE=ONE-66K
SL         CONTINUE
           END OF CONDITIONAL
           END OF CONDITIONAL
          R
           THROUGH DITCH, FOR VALUES OF I=$PROG$,$VAR$,$INS$,
          1$SL/SN$,$MADTAB$
DITCH      WDITCH.(I)
          R
           WORK=NAME
           RTJUST.(WORK)
           FUNCTION RETURN
          R
          RCHECK WHETHER OR NOT A SUBROUTINE CAN BE WORKED
          RON IN CORE.
          R
           ENTRY TO WRKBLE.
           FUNCTION RETURN WRKBL.(NAME)
          R
           INTERNAL FUNCTION (NAME)
           ENTRY TO WRKBL.
          R
           WHENEVER USES(0).E.0
           PRINT.(COM4)
           VECTOR VALUES COM4=33,$GIVE A USE REQUEST AND TRY AGAIN.$
           READY.
           END OF CONDITIONAL
          R
           X=NAME
           RTJUST.(X)
           THROUGH LOOP,FOR I=1,1,I.E.USES(0)
LOOP       WHENEVER USES(I).E.X,FUNCTION RETURN 1B
           FUNCTION RETURN 0B
           END OF FUNCTION
          R
           ENTRY TO FWRKBL.
           WHENEVER WORK.E.$ $,TRANSFER TO ERR1
           WHENEVER WRKBL.(WORK),FUNCTION RETURN
           LIST=0
           ADDCOM.(COM2)
           VECTOR VALUES COM2=37,$ACCORDING TO THE LAST 'USE' REQUEST, $
           ADDNAM.(WORK)
           ADDCOM.(COM3)
           VECTOR VALUES COM3=27,$ IS UNWORKABLE.  TRY AGAIN.$
           CRTYPE.
           READY.
           END OF FUNCTION
